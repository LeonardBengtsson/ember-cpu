# [INFO]
# BUILTIN_FUNC ALT_PARAMS
# SETBITS(startw: usize, startb: usize, endw: usize, endb: usize)
# ALT_PARAMS: (endb, endw, _, startb,startw)
# sets a sequence of bits to true, from bit [startb] in word [startw] to bit [endb] in word [endw], exclusive

# [EXAMPLE]
# startw = 1, startb = 4, endw = 2, endb = 3
#                           ↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓
# IN:  0000100011101101 1001110100011001 1000000011111010
#                           ↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓
# OUT: 0000100011101101 1001111111111111 1111000011111010

# [PSEUDO]
# fn setbits(endb, endw, _, startb, startw)
#   for w in startw..=endw
#     t = read(w)
#     b = 0
#     while b < 16
#       if w == startw && b < startb
#         break
#       if w == endw && b >= endb
#         break
#       b = b + 1
#       t = t | (0x1 << b)
#     write(w, t)

# [.CPU]

# start                 # STACK: startw startb endw endb

# LOOP1 head
.peek 4                 # startw _ _
movab
:LOOP1
movba
.push                   # _ w _, STACK: w ...

# t = read(w)
memr                    # t w _
.push                   # STACK: t w ...

# LOOP2 head
set0x0000               # b _ _
movab                   # _ b _
:LOOP2
movba                   # b _ _
.push                   # STACK: b t w ...

# eval w == startw
.peek 7                 # startw _ _
movac                   # _ _ startw
.peek 2                 # w _ startw
movcb                   # w startw _
movac                   # w startw w
sub                     # (w-startw) _ w
%nz J1                  # w - startw <= 0 (w <= startw)

# eval w >= endw
:J2
.peek 4                 # endw _ w
movcb                   # endw w _
sub
%nz J3                  # endw - w <= 0 (endw <= w)
% J4

# w <= startw
# eval b < startb
:J1
.peek 6                 # startb _ _
movac                   # _ _ startb
.peek                   # b _ startb
movcb                   # b startb _
sub
%n J5                   # b - startb < 0 (b < startb)
.peek 2                 # w _ _
movac                   # _ _ w
% J2

# w == endw
# eval b >= endb
:J3
.peek 3                 # endb _ _
movac                   # _ _ endb
.peek                   # b _ endb
movcb                   # b endb _
sub
%nz J5                  # b - endb <= 0 (b <= endb)

# t = t | (0x1 << b)
:J4
.peek                   # b _ _
movab                   # _ b _
set0x0001               # 0x0001 b _
shl                     # (0x1 << b) _ _
movac                   # _ _ (0x1 << b)
.peek 1                 # t _ (0x1 << b)
movcb                   # t (0x1 << b) _
or                      # t_new _ _
movac                   # _ _ t_new
.rep 1

# LOOP2 end
.pop                    # b _ _
movab                   # b b _
set0x000e               # 0x000e b _
sub
%n J6                   # 14 - b < 0 (14 < b, b+1 >= 16)
movba                   # b _ _
inc                     # b+1 _ _
movab
% LOOP2

# LOOP1 end
:J5
.popn                   # STACK: t w ...
:J6
.pop                    # t _ _
movac                   # _ _ t
.pop                    # w _ t
movcb                   # w t _
memw                    # write(w, t)
movac                   # _ _ w
.peek 1                 # endw _ w
movcb                   # endw w w
sub
%n END                  # endw - w < 0 (endw < w, endw <= w+1)
movca                   # w _ _
inc                     # w+1 _ _
movab                   # _ w+1 _
% LOOP1

:END